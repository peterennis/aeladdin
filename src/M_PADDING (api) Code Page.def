Option Compare Database
Option Explicit

' Call f_CodePage_Init() to feed the global varibales g_CodePageLst() and g_CodePageNbr
' with the Code Pages supported by the current Windows installation.
' 2007-12-27, Skrol29

Private Const MAX_LEADBYTES = 12
Private Const MAX_DEFAULTCHAR = 2
Private Const MAX_PATH = 260

Private Type CPINFOEX
    MaxCharSize As Long ' max length (Byte) of a char
    DefaultChar(0 To MAX_DEFAULTCHAR - 1) As Byte ' default character
    LeadByte(0 To MAX_LEADBYTES - 1) As Byte ' lead byte ranges
    UnicodeDefaultChar As Byte
    CodePage As Long
    CodePageName(0 To MAX_PATH - 1) As Byte
End Type

Private Declare Function api_EnumSystemCodePages Lib "kernel32.dll" Alias "EnumSystemCodePagesA" (ByVal lpCodePageEnumProc As Long, ByVal dwFlags As Long) As Long
Private Declare Sub api_CopyMemory Lib "kernel32" Alias "RtlMoveMemory" (Destination As Any, Source As Any, ByVal Length As Long)
Private Declare Function api_GetCPInfoEx Lib "kernel32" Alias "GetCPInfoExA" (ByVal CodePage As Long, ByVal dwFlags As Long, ByRef lpCPInfoEX As CPINFOEX) As Long

' EnumSystemCodePages
Const CP_INSTALLED = &H1
Const CP_SUPPORTED = &H2

Public Type tCodePage
    id As Long
    Name As String
End Type
Public g_CodePageLst() As tCodePage
Public g_CodePageNbr As Long

Public Function f_CodePage_Init() As Long
    
2       If g_CodePageNbr > 0 Then
3           f_CodePage_Init = g_CodePageNbr
        Exit Function
5       End If
    
'    Call the CallBack as much time as there are CodePage
8       api_EnumSystemCodePages AddressOf f_CodePage_CallBack, CP_INSTALLED
    
10      f_CodePage_Init = g_CodePageNbr
    
End Function

Public Function f_CodePage_CallBack(CP_Pointer As Long) As Long
'    This function is called by the API EnumSystemCodePages to retrieve all Code Pages installed on Windows.
'    Returns TRUE to continue enumeration or FALSE otherwise.
    
4       Dim Buffer As String
5       Dim id As Long
    
7       Buffer = Space$(255)
8       Call api_CopyMemory(ByVal Buffer, CP_Pointer, Len(Buffer))
9       Buffer = Left$(Buffer, InStr(Buffer, Chr$(0)) - 1)
    
11      id = Val(Buffer)
12      If id > 0 Then f_CodePage_Add id, f_CodePage_Name(id)
    
14      f_CodePage_CallBack = 1
    
End Function

Public Sub f_CodePage_Debug()
    
2       Dim i As Long
    
4       For i = 0 To g_CodePageNbr - 1
5           Debug.Print g_CodePageLst(i).id & " : " & g_CodePageLst(i).Name
6       Next i
    
End Sub

Public Sub f_CodePage_Sort()
    
2       Dim i As Long
3       Dim j As Long
4       Dim CodePage As tCodePage
    
6       For i = 0 To g_CodePageNbr - 1
7           j = i
8           Do While j > 0
9               If g_CodePageLst(j).Name < g_CodePageLst(j - 1).Name Then
'                Swap items
11                  CodePage = g_CodePageLst(j - 1)
12                  g_CodePageLst(j - 1) = g_CodePageLst(j)
13                  g_CodePageLst(j) = CodePage
14                  j = j - 1
15              Else
'                End of the loop
17                  j = 0
18              End If
19          Loop
20      Next i
    
End Sub

Public Function f_CodePage_Name(id As Long) As String
'    Returns the name of a given Code Page id.
    
3       Dim CpInfo As CPINFOEX
4       Dim x As String
5       Dim i As Integer
6       Dim Ok As Boolean
    
8       Call api_GetCPInfoEx(id, 0, CpInfo)
    
10      With CpInfo
        
'        Retrieve the full name
13          i = LBound(.CodePageName)
14          Ok = True
15          Do While Ok And (i <= UBound(.CodePageName))
16              If .CodePageName(i) = 0 Then
17                  Ok = False
18              Else
19                  x = x & Chr$(.CodePageName(i))
20                  i = i + 1
21              End If
22          Loop
        
'        Take of the number. Names can be formated like "1234 (name)"
25          i = InStr(x, "(")
26          If (.CodePage > 0) And (i > 0) And (Right(x, 1) = ")") Then
27              If Abs(Val(Left$(x, i - 1)) - .CodePage) <= 2 Then ' we use Abs() becasue the name of 50221 is prefixed with 50220
28                  x = Mid$(x, i + 1)
29                  x = Left$(x, Len(x) - 1)
30              End If
31          End If
        
'        Debug Vista has some Code Pages with no names
34          If x = vbNullString Then
35              If id = 1147 Then x = "IBM EBCDIC (France-Euro)"
36              If id = 20949 Then x = "Korean Wansung"
37          End If
        
'        If no name => display the id
40          If x = vbNullString Then
41              x = "{" & .CodePage & "}"
42          End If
        
44      End With
    
46      f_CodePage_Name = x
    
End Function

Public Function f_CodePage_CurrentId() As Long
'    Returns the id of the Windows current Code Page
    
3       Dim CpInfo As CPINFOEX
    
5       Call api_GetCPInfoEx(0, 0, CpInfo)
    
7       f_CodePage_CurrentId = CpInfo.CodePage
    
    
End Function

Public Function f_CodePage_Add(id As Long, Name As String) As Long
'    Add a new code page in the list, and return the index if the item
    
3       ReDim Preserve g_CodePageLst(0 To g_CodePageNbr)
    
5       With g_CodePageLst(g_CodePageNbr)
6           .id = id
7           .Name = Name
8       End With
    
10      f_CodePage_Add = g_CodePageNbr
    
12      g_CodePageNbr = g_CodePageNbr + 1
    
End Function

Public Function f_CodePage_Index(id As Long) As Long
'    Return the index of the code page in the list, returns -1 if the code page does not exist in the list.
    
3       Dim i As Long
4       Dim Ok As Boolean
    
6       i = 0
7       Do Until Ok Or (i >= g_CodePageNbr)
8           If g_CodePageLst(i).id = id Then
9               Ok = True
10          Else
11              i = i + 1
12          End If
13      Loop
    
15      If Ok Then
16          f_CodePage_Index = i
17      Else
18          f_CodePage_Index = -1
19      End If
    
End Function